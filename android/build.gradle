buildscript {
  repositories {
    google()
    jcenter()
  }
  dependencies {
    classpath rootProject.ext.has('gradleBuildTools') ? rootProject.ext.get('gradleBuildTools') : 'com.android.tools.build:gradle:3.4.2'
    classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  compileSdkVersion safeExtGet('compileSdkVersion', 28)
  buildToolsVersion safeExtGet('buildToolsVersion', '28.0.3')

  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 16)
    targetSdkVersion safeExtGet('targetSdkVersion', 22)
    versionCode 1
    versionName "1.0"
  }
  lintOptions {
    abortOnError false
  }
}

repositories {
  mavenLocal()
  google()
  jcenter()
  maven { url "https://jitpack.io" }
  if (project == rootProject) {
    maven {
      url "https://a8c-libs.s3.amazonaws.com/android/react-native-mirror"
    }
  } else {
    maven {
      // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
      url "$projectDir/../../../node_modules/react-native/android"
    }
  }
}

dependencies {
  if (project == rootProject) {
    // If this is the root project (e.g. Jitpack), specify a version
    implementation 'com.facebook.react:react-native:0.61.5'
  } else {
    implementation 'com.facebook.react:react-native:+'
  }
  implementation fileTree( dir: "libs", includes: ['*.jar'] )
}

