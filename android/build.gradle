plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "maven-publish"
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    if (project == rootProject) {
        maven { url "https://a8c-libs.s3.amazonaws.com/android/react-native-mirror" }
    } else {
        // When building as a dep, the RN's maven repo is locally in the node_modules folder
        def nodeModulesPath = "${project.buildDir}/../../node_modules/"
        maven { url "${nodeModulesPath}/react-native/android" }
    }

    google()
    mavenCentral()
    jcenter()
}

dependencies {
    if (project == rootProject) {
        // If this is the root project (e.g. Jitpack), specify a version
        implementation 'com.facebook.react:react-native:0.66.2'
    } else {
        //noinspection GradleDynamicVersion
        api "com.facebook.react:react-native:+"
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.wordpress-mobile'
                artifactId = 'react-native-webview'
            }
        }
    }
}
